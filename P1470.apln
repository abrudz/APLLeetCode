:Namespace P1470
    ⍝ LeetCode problem #1470
    ⍝ Shuffle the Array, https://leetcode.com/problems/shuffle-the-array/
    (⎕IO ⎕ML) ← 0 1
    
    Sol ← {
        ⍝ Reshape into two rows, then ravel the transpose.
        ,⍉2(2÷⍨≢⍵)⍴⍵
    }
    
    ⍝ Tacit solution implementing same algorithm as above.
    Sol2 ← (,∘⍉⊢⍴⍨2,(÷∘2)∘≢)
    
    ⍝ by @abrudz from here on
    ⍝ Tacit solution more in the style of Sol.
    Sol3 ← (,∘⍉(2,2÷⍨≢)⍴⊢)
    ⍝ Both Sol and Sol3 can be written without parentheses as `{,⍉⍵⍴⍨2,2÷⍨≢⍵}` and `(,∘⍉⊢⍴⍨2,2÷⍨≢)` respectively

    ⍝ Different approach: get the indices mod `n`.
    Sol4 ← {⍵[⍋(÷∘2|⍳)≢⍵]}

    ⍝ The problem statement actually seems to provide `n` so if we take this as left argument:
    Sol1n ← {,⍉2 ⍺⍴⍵}
    Sol2n ← (,∘⍉⊢⍴⍨2,⊣)
    Sol3n ← (,∘⍉(2,⊣)⍴⊢)
    Sol4n ← {⍵[⍋⍺|⍳≢⍵]}
    
    ⍝ If we instead take `(nums n)` as right argument, we can write a short tacit solution:
    Sol5 ← ,∘⍉2@0⍴⊃
    ⍝ `2@0` replaces `nums` with `2` giving `2 n` and this is then used to reshape `⊃(nums n)` i.e. `nums`
     
:EndNamespace
